http = require "http"
url = require "url"


class Router

    constructor: (@default_handler) ->
        @handlers = {}

    add_view: (route, handler) ->
        @handlers[route] = handler

    dispatch: (route, request, response) ->
        if route of @handlers
            @handlers[route](request, response)
        else
            @default_handler(request, response)


class WebServer

    constructor: (@port, @router) ->

    dispatch: (request, response) =>
        pathname = url.parse(request.url).pathname
        @router.dispatch(pathname, request, response)

    run: () ->
        http.createServer(@dispatch).listen(@port);


default_handler = (request, response) ->
    response.writeHead 200, {"Content-Type": "text/plain"}
    response.write "Default handler"
    response.end()

first_handler = (request, response) ->
    response.writeHead 200, {"Content-Type": "text/plain"}
    response.write "firsts handler"
    response.end()

router = new Router default_handler
router.add_view("/", first_handler)

instance = new WebServer 8001, router
instance.run()